// Copyright (c) 2020 Princeton University
// All rights reserved.
// 
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions are met:
//     * Redistributions of source code must retain the above copyright
//       notice, this list of conditions and the following disclaimer.
//     * Redistributions in binary form must reproduce the above copyright
//       notice, this list of conditions and the following disclaimer in the
//       documentation and/or other materials provided with the distribution.
//     * Neither the name of Princeton University nor the
//       names of its contributors may be used to endorse or promote products
//       derived from this software without specific prior written permission.
// 
// THIS SOFTWARE IS PROVIDED BY PRINCETON UNIVERSITY "AS IS" AND
// ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
// WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
// DISCLAIMED. IN NO EVENT SHALL PRINCETON UNIVERSITY BE LIABLE FOR ANY
// DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
// (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
// LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
// ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
// (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
// SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

// Filename: noc_fbits_splitter.v
// Author: Fei Gao
// Description: Split the noc message to different destination based on the fbits

`include "define.tmp.h"
`include "network_define.v"

// NoC msg with unspecified fbits will not be received: 
// the ready signal keeps low, and it will block the router
module noc_fbits_splitter (
    input                               clk,
    input                               rst_n,

    input   [2:0]                       num_targets,

    input                               src_splitter_vr_noc_val,
    input       [`NOC_DATA_WIDTH-1:0]   src_splitter_vr_noc_dat,
    output reg                          src_splitter_vr_noc_rdy,

    output reg                          splitter_dst0_vr_noc_val,
    output      [`NOC_DATA_WIDTH-1:0]   splitter_dst0_vr_noc_dat,
    input                               splitter_dst0_vr_noc_rdy,
    input      [`NOC_FBITS_WIDTH-1:0]   fbits_type0,    // Processor

    output reg                          splitter_dst1_vr_noc_val,
    output      [`NOC_DATA_WIDTH-1:0]   splitter_dst1_vr_noc_dat,
    input                               splitter_dst1_vr_noc_rdy,
    input      [`NOC_FBITS_WIDTH-1:0]   fbits_type1,

    output reg                          splitter_dst2_vr_noc_val,
    output      [`NOC_DATA_WIDTH-1:0]   splitter_dst2_vr_noc_dat,
    input                               splitter_dst2_vr_noc_rdy,
    input      [`NOC_FBITS_WIDTH-1:0]   fbits_type2,

    output reg                          splitter_dst3_vr_noc_val,
    output      [`NOC_DATA_WIDTH-1:0]   splitter_dst3_vr_noc_dat,
    input                               splitter_dst3_vr_noc_rdy,
    input      [`NOC_FBITS_WIDTH-1:0]   fbits_type3,

    output reg                          splitter_dst4_vr_noc_val,
    output      [`NOC_DATA_WIDTH-1:0]   splitter_dst4_vr_noc_dat,
    input                               splitter_dst4_vr_noc_rdy,
    input      [`NOC_FBITS_WIDTH-1:0]   fbits_type4         // Maximum target number is fixed to 5

);

    localparam IDLE = 3'd0;
    localparam COUNT_TYPE0 = 3'd1;
    localparam COUNT_TYPE1 = 3'd2;
    localparam COUNT_TYPE2 = 3'd3;
    localparam COUNT_TYPE3 = 3'd4;
    localparam COUNT_TYPE4 = 3'd5;
    
    reg [2:0] state_reg;
    reg [2:0] state_next;
    
    reg [`MSG_LENGTH_WIDTH-1:0] count_reg;
    reg [`MSG_LENGTH_WIDTH-1:0] count_next;
    
    always @(posedge clk) begin
        if (~rst_n) begin
            state_reg <= IDLE;
            count_reg <= {`MSG_LENGTH_WIDTH{1'b0}};
        end
        else begin
            state_reg <= state_next;
            if ((state_next >= COUNT_TYPE0) && (state_next <= COUNT_TYPE4)) begin
                count_reg <= count_next;
            end
        end
    end
    
    assign splitter_dst0_vr_noc_dat = src_splitter_vr_noc_dat;
    assign splitter_dst1_vr_noc_dat = src_splitter_vr_noc_dat;
    assign splitter_dst2_vr_noc_dat = src_splitter_vr_noc_dat;
    assign splitter_dst3_vr_noc_dat = src_splitter_vr_noc_dat;
    assign splitter_dst4_vr_noc_dat = src_splitter_vr_noc_dat;
    
    always @* begin
        splitter_dst0_vr_noc_val = 0;    
        splitter_dst1_vr_noc_val = 0;
        splitter_dst2_vr_noc_val = 0;
        splitter_dst3_vr_noc_val = 0;
        splitter_dst4_vr_noc_val = 0;
    
        case (state_reg)
        IDLE: begin
    
            count_next = src_splitter_vr_noc_val & |src_splitter_vr_noc_dat[`MSG_LENGTH] ?
                            src_splitter_vr_noc_dat[`MSG_LENGTH] :
                            {`MSG_LENGTH_WIDTH{1'b0}};
            
            splitter_dst0_vr_noc_val = src_splitter_vr_noc_val & (src_splitter_vr_noc_dat[`MSG_DST_FBITS] == fbits_type0);
            splitter_dst1_vr_noc_val = src_splitter_vr_noc_val & (src_splitter_vr_noc_dat[`MSG_DST_FBITS] == fbits_type1) & (num_targets > 1) 
                                        & ~splitter_dst0_vr_noc_val;
            splitter_dst2_vr_noc_val = src_splitter_vr_noc_val & (src_splitter_vr_noc_dat[`MSG_DST_FBITS] == fbits_type2) & (num_targets > 2)
                                        & ~splitter_dst0_vr_noc_val
                                        & ~splitter_dst1_vr_noc_val;
            splitter_dst3_vr_noc_val = src_splitter_vr_noc_val & (src_splitter_vr_noc_dat[`MSG_DST_FBITS] == fbits_type3) & (num_targets > 3)
                                        & ~splitter_dst0_vr_noc_val
                                        & ~splitter_dst1_vr_noc_val
                                        & ~splitter_dst2_vr_noc_val;
            splitter_dst4_vr_noc_val = src_splitter_vr_noc_val & (src_splitter_vr_noc_dat[`MSG_DST_FBITS] == fbits_type4) & (num_targets > 4)
                                        & ~splitter_dst0_vr_noc_val
                                        & ~splitter_dst1_vr_noc_val
                                        & ~splitter_dst2_vr_noc_val
                                        & ~splitter_dst3_vr_noc_val;                      // Conservative design, in case different fbits_types are set to a same value
    
            src_splitter_vr_noc_rdy =  (splitter_dst0_vr_noc_val & splitter_dst0_vr_noc_rdy) | 
                                    (splitter_dst1_vr_noc_val & splitter_dst1_vr_noc_rdy) |
                                    (splitter_dst2_vr_noc_val & splitter_dst2_vr_noc_rdy) |
                                    (splitter_dst3_vr_noc_val & splitter_dst3_vr_noc_rdy) |
                                    (splitter_dst4_vr_noc_val & splitter_dst4_vr_noc_rdy) ;
    
            state_next =    (|src_splitter_vr_noc_dat[`MSG_LENGTH] == 0) ? (IDLE) :
                            (splitter_dst0_vr_noc_val & splitter_dst0_vr_noc_rdy) ? (COUNT_TYPE0) :
                            (splitter_dst1_vr_noc_val & splitter_dst1_vr_noc_rdy) ? (COUNT_TYPE1) :
                            (splitter_dst2_vr_noc_val & splitter_dst2_vr_noc_rdy) ? (COUNT_TYPE2) :
                            (splitter_dst3_vr_noc_val & splitter_dst3_vr_noc_rdy) ? (COUNT_TYPE3) :
                            (splitter_dst4_vr_noc_val & splitter_dst4_vr_noc_rdy) ? (COUNT_TYPE4) :
                            (IDLE);
        end
<%
t = '''
    
        COUNT_TYPE_TARGETID: begin
            splitter_dst_TARGETID_vr_noc_val = src_splitter_vr_noc_val;
            src_splitter_vr_noc_rdy = splitter_dst_TARGETID_vr_noc_rdy;
    
            count_next = (splitter_dst_TARGETID_vr_noc_val & splitter_dst_TARGETID_vr_noc_rdy) ? (count_reg - 1'b1) : count_reg;
            state_next = (count_next == {`MSG_LENGTH_WIDTH{1'b0}}) ? IDLE : COUNT_TYPE_TARGETID;
        end
'''

for i in range(5):                             # Maximum target number fixed to 5
    tt = t.replace("_TARGETID", str(i))
    print(tt)
%>
    
        default: begin
            count_next = {`MSG_LENGTH_WIDTH{1'b0}};
            src_splitter_vr_noc_rdy = 1'b0;
            state_next = IDLE;
        end
        endcase
    end
    
endmodule
